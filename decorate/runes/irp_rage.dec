actor IRP_RageRune : IRP_BaseRune replaces RageRune
{
    Game Doom
    Game Heretic
    Game Hexen
    SpawnID 181
    //+COUNTITEM // disabled due to permarunes setting
    +NOGRAVITY
    States
    {
      Spawn:
        RAGR ABCDEFGH 6
        loop

      PickupDefault:
        TNT1 A 0 A_JumpIf(CallACS("IRP_Decorate", 1), 2)
        TNT1 A 0 A_JumpIfInventory("RuneDoubleFiringSpeed",1,"PickupFail")
        goto PickupDisplay
        TNT1 A 0 A_JumpIf(CallACS("IRP_HoldableLimitCheck", 1), "PickupFail")
        goto PickupDisplay

      PickupDisplayDefault:
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_PickupMessages", 1)
        TNT1 A 0 A_SetBlend("D7BA45",0.3,4)
        TNT1 A 0 A_PlaySound("misc/p_pkup",CHAN_ITEM|CHAN_MAYBE_LOCAL)
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_AnnouncerSounds", 1)
        goto PickupSuccess

      PickupDisplaySamsaraHeretic:
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_PickupMessages", 1)
        TNT1 A 0 A_SetBlend("D7BA45",0.3,4)
        TNT1 A 0 A_PlaySound("heretic/itemget",CHAN_ITEM|CHAN_MAYBE_LOCAL)
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_AnnouncerSounds", 1)
        goto PickupSuccess

      PickupSuccessDefault:
        TNT1 A 0 A_JumpIf(CallACS("IRP_Decorate", 1), 2)
        TNT1 A 0 A_GiveInventory("RageRune")
        goto PickupFinish
        TNT1 A 0 A_GiveInventory("IRP_RageRuneHoldable")
        goto PickupFinish
    }
}

actor IRP_RageRuneHoldable : IRP_BaseRuneHoldable
{
    Inventory.Icon "ARTIRNE1"
    Tag "$IRP_TAG_RAGE"
    States
    {
      UseDefault:
        TNT1 A 0 A_JumpIfInventory("RuneDoubleFiringSpeed",1,"UseFail")
        goto UsePlaySound

      UseSuccessDefault:
        TNT1 A 0 A_GiveInventory("RageRune")
        goto UseFinish
    }
}
