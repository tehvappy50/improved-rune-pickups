actor IRP_ResistanceRune : IRP_BaseRune replaces ResistanceRune
{
    Game Doom
    Game Heretic
    Game Hexen
    SpawnID 184
    //+COUNTITEM // disabled due to permarunes setting
    +NOGRAVITY
    States
    {
      Spawn:
        RESR ABCDEFGH 6
        loop

      PickupDefault:
        TNT1 A 0 A_JumpIf(CallACS("IRP_Decorate", 1), 2)
        TNT1 A 0 A_JumpIfInventory("RuneHalfDamage",1,"PickupFail")
        goto PickupDisplay
        TNT1 A 0 A_JumpIf(CallACS("IRP_HoldableLimitCheck", 4), "PickupFail")
        goto PickupDisplay

      PickupDisplayDefault:
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_PickupMessages", 4)
        TNT1 A 0 A_SetBlend("D7BA45",0.3,4)
        TNT1 A 0 A_PlaySound("misc/p_pkup",CHAN_ITEM|CHAN_MAYBE_LOCAL)
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_AnnouncerSounds", 4)
        goto PickupSuccess

      PickupDisplaySamsaraHeretic:
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_PickupMessages", 4)
        TNT1 A 0 A_SetBlend("D7BA45",0.3,4)
        TNT1 A 0 A_PlaySound("heretic/itemget",CHAN_ITEM|CHAN_MAYBE_LOCAL)
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_AnnouncerSounds", 4)
        goto PickupSuccess

      PickupSuccessDefault:
        TNT1 A 0 A_JumpIf(CallACS("IRP_Decorate", 1), 2)
        TNT1 A 0 A_GiveInventory("ResistanceRune")
        goto PickupFinish
        TNT1 A 0 A_GiveInventory("IRP_ResistanceRuneHoldable")
        goto PickupFinish
    }
}

actor IRP_ResistanceRuneHoldable : IRP_BaseRuneHoldable
{
    Inventory.Icon "ARTIRNE4"
    Tag "$IRP_TAG_RESISTANCE"
    States
    {
      UseDefault:
        TNT1 A 0 A_JumpIfInventory("RuneHalfDamage",1,"UseFail")
        goto UsePlaySound

      UseSuccessDefault:
        TNT1 A 0 A_GiveInventory("ResistanceRune")
        goto UseFinish
    }
}
