actor IRP_ReflectionRune : IRP_BaseRune replaces ReflectionRune
{
    Game Doom
    Game Heretic
    Game Hexen
    SpawnID 187
    //+COUNTITEM // disabled due to permarunes setting
    +FLOATBOB
    +NOGRAVITY
    States
    {
      Spawn:
        REFR BA 6
        loop

      PickupDefault:
        TNT1 A 0 A_JumpIf(CallACS("IRP_Decorate", 1), 2)
        TNT1 A 0 A_JumpIfInventory("RuneReflection",1,"PickupFail")
        goto PickupDisplay
        TNT1 A 0 A_JumpIf(CallACS("IRP_HoldableLimitCheck", 7), "PickupFail")
        goto PickupDisplay

      PickupDisplayDefault:
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_PickupMessages", 7)
        TNT1 A 0 A_SetBlend("D7BA45",0.3,4)
        TNT1 A 0 A_PlaySound("misc/p_pkup",CHAN_ITEM|CHAN_MAYBE_LOCAL)
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_AnnouncerSounds", 7)
        goto PickupSuccess

      PickupDisplaySamsaraHeretic:
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_PickupMessages", 7)
        TNT1 A 0 A_SetBlend("D7BA45",0.3,4)
        TNT1 A 0 A_PlaySound("heretic/itemget",CHAN_ITEM|CHAN_MAYBE_LOCAL)
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_AnnouncerSounds", 7)
        goto PickupSuccess

      PickupSuccessDefault:
        TNT1 A 0 A_JumpIf(CallACS("IRP_Decorate", 1), 2)
        TNT1 A 0 A_GiveInventory("ReflectionRune")
        goto PickupFinish
        TNT1 A 0 A_GiveInventory("IRP_ReflectionRuneHoldable")
        goto PickupFinish
    }
}

actor IRP_ReflectionRuneHoldable : IRP_BaseRuneHoldable
{
    Inventory.Icon "ARTIRNE7"
    Tag "$IRP_TAG_REFLECTION"
    States
    {
      UseDefault:
        TNT1 A 0 A_JumpIfInventory("RuneReflection",1,"UseFail")
        goto UsePlaySound

      UseSuccessDefault:
        TNT1 A 0 A_GiveInventory("ReflectionRune")
        goto UseFinish
    }
}
