actor IRP_RegenerationRune : IRP_BaseRune replaces RegenerationRune
{
    Game Doom
    Game Heretic
    Game Hexen
    SpawnID 185
    //+COUNTITEM // disabled due to permarunes setting
    +FLOATBOB
    +NOGRAVITY
    States
    {
      Spawn:
        REGR AB 6
        loop

      PickupDefault:
        TNT1 A 0 A_JumpIf(CallACS("IRP_Decorate", 1), 2)
        TNT1 A 0 A_JumpIfInventory("RuneRegeneration",1,"PickupFail")
        goto PickupDisplay
        TNT1 A 0 A_JumpIf(CallACS("IRP_HoldableLimitCheck", 5), "PickupFail")
        goto PickupDisplay

      PickupDisplayDefault:
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_PickupMessages", 5)
        TNT1 A 0 A_SetBlend("D7BA45",0.3,4)
        TNT1 A 0 A_PlaySound("misc/p_pkup",CHAN_ITEM|CHAN_MAYBE_LOCAL)
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_AnnouncerSounds", 5)
        goto PickupSuccess

      PickupDisplaySamsaraHeretic:
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_PickupMessages", 5)
        TNT1 A 0 A_SetBlend("D7BA45",0.3,4)
        TNT1 A 0 A_PlaySound("heretic/itemget",CHAN_ITEM|CHAN_MAYBE_LOCAL)
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_AnnouncerSounds", 5)
        goto PickupSuccess

      PickupSuccessDefault:
        TNT1 A 0 A_JumpIf(CallACS("IRP_Decorate", 1), 2)
        TNT1 A 0 A_GiveInventory("RegenerationRune")
        goto PickupFinish
        TNT1 A 0 A_GiveInventory("IRP_RegenerationRuneHoldable")
        goto PickupFinish
    }
}

actor IRP_RegenerationRuneHoldable : IRP_BaseRuneHoldable
{
    Inventory.Icon "ARTIRNE5"
    Tag "$IRP_TAG_REGENERATION"
    States
    {
      UseDefault:
        TNT1 A 0 A_JumpIfInventory("RuneRegeneration",1,"UseFail")
        goto UsePlaySound

      UseSuccessDefault:
        TNT1 A 0 A_GiveInventory("RegenerationRune")
        goto UseFinish
    }
}
