actor IRP_BaseRune : CustomInventory
{
    +QUIET
    States
    {
      Pickup:
        TNT1 A 0 A_Jump(256,"PickupDefault") // for inheriting actors
        goto PickupDefault

      PickupDefault:
        goto PickupDisplay

      PickupDisplay:
        TNT1 A 0 A_JumpIf(CallACS("IRP_CompatCheck_SamsaraHeretic"), "PickupDisplaySamsaraHeretic")
        TNT1 A 0 A_Jump(256,"PickupDisplayDefault") // for inheriting actors
        goto PickupDisplayDefault

      PickupDisplayDefault:
      PickupDisplaySamsaraHeretic:
        goto PickupSuccess

      PickupSuccess:
        TNT1 A 0 A_Jump(256,"PickupSuccessDefault") // for inheriting actors
        goto PickupSuccessDefault

      PickupSuccessDefault:
        goto PickupFinish

      PickupFinish:
        TNT1 A 0 A_JumpIf(CallACS("IRP_Decorate", 0), "PickupFail")
        stop

      PickupFail:
        TNT1 A 0
        fail

      PickupStop:
        TNT1 A 0
        stop
    }
}

actor IRP_BaseRuneHoldable : CustomInventory
{
    Inventory.UseSound ""
    Inventory.MaxAmount 0x7FFFFFFF
    //Inventory.InterHubAmount 0x7FFFFFFF
    +INVBAR
    +UNTOSSABLE
    States
    {
      Use:
        TNT1 A 0 A_Jump(256,"UseDefault") // for inheriting actors
        goto UseDefault

      UseDefault:
        goto UsePlaySound

      UsePlaySound:
        TNT1 A 0 A_JumpIf(CallACS("IRP_CompatCheck_SamsaraHeretic"), "UsePlaySoundSamsaraHeretic")
        TNT1 A 0 A_Jump(256,"UsePlaySoundDefault") // for inheriting actors
        goto UsePlaySoundDefault

      UsePlaySoundDefault:
        TNT1 A 0 A_PlaySound("misc/invuse",CHAN_ITEM)
        goto UseSuccess

      UsePlaySoundSamsaraHeretic:
        TNT1 A 0 A_PlaySound("heretic/itemuse",CHAN_ITEM)
        goto UseSuccess

      UseSuccess:
        TNT1 A 0 A_Jump(256,"UseSuccessDefault") // for inheriting actors
        goto UseSuccessDefault

      UseSuccessDefault:
        goto UseFinish

      UseFinish:
        TNT1 A 0 A_JumpIf(CallACS("IRP_Decorate", 0), "UseFail")
        TNT1 A 0 A_RailWait
        stop

      UseFail:
        TNT1 A 0
        fail
    }
}
