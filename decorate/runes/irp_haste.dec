actor IRP_HasteRune : IRP_BaseRune replaces HasteRune
{
    Game Doom
    Game Heretic
    Game Hexen
    SpawnID 189
    //+COUNTITEM // disabled due to permarunes setting
    +FLOATBOB
    +NOGRAVITY
    States
    {
      Spawn:
        HASR ABCD 6
        loop

      PickupDefault:
        TNT1 A 0 A_JumpIf(CallACS("IRP_Decorate", 1), 2)
        TNT1 A 0 A_JumpIfInventory("RuneSpeed25",1,"PickupFail")
        goto PickupDisplay
        TNT1 A 0 A_JumpIf(CallACS("IRP_HoldableLimitCheck", 9), "PickupFail")
        goto PickupDisplay

      PickupDisplayDefault:
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_PickupMessages", 9)
        TNT1 A 0 A_SetBlend("D7BA45",0.3,4)
        TNT1 A 0 A_PlaySound("misc/p_pkup",CHAN_ITEM|CHAN_MAYBE_LOCAL)
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_AnnouncerSounds", 9)
        goto PickupSuccess

      PickupDisplaySamsaraHeretic:
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_PickupMessages", 9)
        TNT1 A 0 A_SetBlend("D7BA45",0.3,4)
        TNT1 A 0 A_PlaySound("heretic/itemget",CHAN_ITEM|CHAN_MAYBE_LOCAL)
        TNT1 A 0 ACS_NamedExecuteWithResult("IRP_AnnouncerSounds", 9)
        goto PickupSuccess

      PickupSuccessDefault:
        TNT1 A 0 A_JumpIf(CallACS("IRP_Decorate", 1), 2)
        TNT1 A 0 A_GiveInventory("HasteRune")
        goto PickupFinish
        TNT1 A 0 A_GiveInventory("IRP_HasteRuneHoldable")
        goto PickupFinish
    }
}

actor IRP_HasteRuneHoldable : IRP_BaseRuneHoldable
{
    Inventory.Icon "ARTIRNE9"
    Tag "$IRP_TAG_HASTE"
    States
    {
      UseDefault:
        TNT1 A 0 A_JumpIfInventory("RuneSpeed25",1,"UseFail")
        goto UsePlaySound

      UseSuccessDefault:
        TNT1 A 0 A_GiveInventory("HasteRune")
        goto UseFinish
    }
}
